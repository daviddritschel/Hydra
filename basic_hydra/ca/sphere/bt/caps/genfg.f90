program genfg

! Program for creating an ultra-high resolution image of the vorticity field
! or a portion thereof from data generated by the hydra/ca/sphere/bt/ 
! suite of f90 codes.

use contours
use congen

implicit double precision(a-h,o-z)
implicit integer(i-n)

 !Local declarations:
double precision:: qq0(0:ng,nt)
double precision:: xtd(nt),utd(nt)
double precision:: etdu(ngu),htdu(ngu)
double precision:: rhs(ngu)
real:: tin,qin(ng,nt),rqa(ngu,ntu),rrqa(ngu/2,ntu/2)
integer(kind=dbleint):: npu
character(len=3):: pind

!-----------------------------------------------------------------
write(*,'(2(a,i3))') ' There are ',nperiod, &
                   & ' periods ranging from 0 to ',nperiod
write(*,'(a,f5.1,a)') ' Each lasts ',tsim,' units of time.'
call init_contours
call init_spectral 

 !Initialise 4th order interpolation 
 !(full to half-grid) coefficients:
htdu(1)=one/f74
etdu(1)=-f14*htdu(1)
do j=2,ngu-1
  htdu(j)=one/(f32+f14*etdu(j-1))
  etdu(j)=-f14*htdu(j)
enddo
htdu(ngu)=one/(f74+f14*etdu(ngu-1))

!------------------------------------------------------------
write(*,*)
write(*,*) ' Which period do you wish to image?'
read(*,*) iind

write(*,'(a,f9.1,a)') ' *** Imaging t = ',tsim*dble(iind)

write(pind(1:3),'(i3.3)') iind
loop=iind+1

 !Open residual needed to build ultra-fine-grid vorticity with congen:
open(40,file='cont/qd.r4',form='unformatted', &
    & access='direct',status='old',recl=nbytes)
read(40,rec=loop) tin,qin
qd=dble(qin)
close(40) 

 !Read PV (absolute vorticity) contours:
open(40,file='cont/synopsis.asc',status='old')
do i=1,loop
  read(40,*) t,n,npt
enddo
close(40)

open(40,file='cont/index'//pind,form='unformatted',status='old')
read(40) np(1:n),i1(1:n),ind(1:n)
close(40)

open(40,file='cont/nodes'//pind,form='unformatted',status='old')
read(40) x(1:npt),y(1:npt),z(1:npt)
close(40)

 !Reconstruct next array:
do j=1,n
  ibeg=i1(j)
  iend=ibeg+np(j)-1
  do i=ibeg,iend-1
    next(i)=i+1
  enddo
  next(iend)=ibeg
enddo 

!----------------------------------------------------------------
 !Loop over great circles in latitude (only half the longitudes):
do ix=1,ng
  ic=ix+ng

   !Source vector:
  utd(1)=f23*(qd(1,ix)+qd(1,ic))
  do j=2,ng
    utd(j)=f23*(qd(j,ix)+qd(j-1,ix))
  enddo
  utd(ngp1)=f23*(qd(ng,ic)+qd(ng,ix))
  do j=ngp2,nt
    utd(j)=f23*(qd(ntp2-j,ic)+qd(ntp1-j,ic))
  enddo

   !Interpolate qd by 4th-order method (periodic):
  xtd(1)=utd(1)*htd(1)
  do j=2,nt
    xtd(j)=(utd(j)-f16*xtd(j-1))*htd(j)
  enddo
  do j=ntm2,1,-1
    xtd(j)=etd(j)*xtd(j+1)+xtd(j)
  enddo
  xtd(nt)=(etd(nt)*xtd(1)+xtd(nt))*xndeno
  xend=xtd(nt)

  do j=1,ntm1
    xtd(j)=ptd(j)*xend+xtd(j)
  enddo

   !Copy back into full grid array (qq0):
  do j=0,ng
    qq0(j,ix)=xtd(j+1)
  enddo
  qq0(0,ic)=xtd(1)
  do j=1,ng
    qq0(j,ic)=xtd(ntp1-j)
  enddo

enddo
 !Ends loops over great circles.  Interpolation complete.

 !Obtain unique polar values of qd for use below:
qdsp=zero
qdnp=zero
do ix=1,nt
  qdsp=qdsp+qq0(0 ,ix)
  qdnp=qdnp+qq0(ng,ix)
enddo
qdsp=qdsp/dble(nt)
qdnp=qdnp/dble(nt)
do ix=1,nt
  qq0(0 ,ix)=qdsp
  qq0(ng,ix)=qdnp
enddo

!------------------------------------------------------------------
 !Obtain gridded PV (qa) from contours:
call con2ugrid

 !Bi-linear interpolate qd (in qq0) to the fine grid and add to qa:
do ix=1,ntu
  ixf=ixfw(ix)
  ix0=ix0w(ix)
  ix1=ix1w(ix)

  qa(0,ix)=qa(0,ix)+qdsp
  do iy=1,ngu-1
    iyf=iyfw(iy)
    iy0=iy0w(iy)
    iy1=iy1w(iy)

    qa(iy,ix)=qa(iy,ix)+w00(iyf,ixf)*qq0(iy0,ix0)+w10(iyf,ixf)*qq0(iy1,ix0) &
                     & +w01(iyf,ixf)*qq0(iy0,ix1)+w11(iyf,ixf)*qq0(iy1,ix1)
  enddo
  qa(ngu,ix)=qa(ngu,ix)+qdnp
enddo
 !qdsp & qdnp are the polar qd values (necessarily uniform).

!------------------------------------------------------------
 !Remove global average:
avqa=zero
do ix=1,ntu
  do iy=1,ngu-1
    avqa=avqa+qa(iy,ix)*rdtu(iy)
  enddo
enddo
avqa=avqa*dsumui

 !Also remove f if omega .ne. 0:
if (omega .ne. zero) then
  aspsqm1=asp**2-one
  do ix=1,ntu
    do iy=0,ngu
      rlatu=dlu*dble(iy)-hpi
      qa(iy,ix)=qa(iy,ix)-avqa-fpole*sin(rlatu)/sqrt(one+aspsqm1*cos(rlatu)**2)
    enddo
  enddo
else
  do ix=1,ntu
    do iy=0,ngu
      qa(iy,ix)=qa(iy,ix)-avqa
    enddo
  enddo
endif

!------------------------------------------------------------
 !Perform 4th order compact interpolation
 !to get qa on the half-grid in latitude:
do ix=1,ntu
  rhs(1)=qa(1,ix)
  do iy=2,ngu-1
    rhs(iy)=qa(iy,ix)+qa(iy-1,ix)
  enddo
  rhs(ngu)=qa(ngu-1,ix)

  qa(1,ix)=rhs(1)*htdu(1)
  do iy=2,ngu
    qa(iy,ix)=(rhs(iy)-f14*qa(iy-1,ix))*htdu(iy)
  enddo

  do iy=ngu-1,1,-1
    qa(iy,ix)=etdu(iy)*qa(iy+1,ix)+qa(iy,ix)
  enddo
enddo

do ix=1,ntu
  do iy=1,ngu
    rqa(iy,ix)=real(qa(iy,ix))
  enddo
enddo

 !Open output file:
open(44,file='fine/qq'//pind//'.r4',form='unformatted', &
      access='stream',status='replace')

write(*,*)
write(*,*) ' Choose the type of image:'
write(*,*) '   (1) Full domain view using all data points'
write(*,*) '   (2) As above, but using every nth point'
write(*,*) '   (3) Partial domain view'
read(*,*) iopt

write(*,*) ' Writing fine/qq'//pind//'.r4'
if (iopt .eq. 1) then
  nxu=ntu
  nyu=ngu
  write(44) real(t),rqa
else if (iopt .eq. 2) then
  write(*,*) ' Enter the stride, n:'
  read(*,*) inc
  nxu=ntu/inc
  nyu=ngu/inc
  do ix=1,nxu
    ixo=(ix-1)*inc+1
    do iy=1,nyu
      iyo=(iy-1)*inc+1
      rrqa(iy,ix)=rqa(iyo,ixo)
    enddo
  enddo
  write(44) real(t),rrqa(1:nyu,1:nxu)
else
  write(*,'(a,i5,a)') ' There are ',nt,' longitudes.'
  write(*,*) ' Range of longitude grid points (i1,i2) on original grid?'
  write(*,*) ' (Periodic wrapping is allowed => i2 > i1)'
  read(*,*) ix1,ix2
  ix1u=(ix1-1)*mgu+1
  ix2u=ix2*mgu
   !Allow periodic wrapping:
  nxu=mod(ix2u-ix1u+ntu,ntu)+1
  write(*,'(a,i5,a)') ' There are ',ng,' latitudes.'
  write(*,*) ' Range of latitude grid points (j1,j2) on original grid?'
  read(*,*) iy1,iy2
  iy1u=(iy1-1)*mgu+1
  iy2u=iy2*mgu
  nyu=iy2u-iy1u+1
  if (ix1u .lt. ix2u) then
    write(44) real(t),rqa(iy1u:iy2u,ix1u:ix2u)
  else
    write(44) real(t),rqa(iy1u:iy2u,ix1u:ntu),rqa(iy1u:iy2u,1:ix2u)
  endif
endif

close(44)

write(*,*)
write(*,*) ' Image by typing the command'
write(*,*)
write(*,'(a,i5,1x,i5)') ' dataview fine/qq'//pind//'.r4 -ndim ',nxu,nyu
write(*,*)

end program
