program enstrophu
!  -------------------------------------------------------------------------
!  |   Computes the enstrophy <q^2>/2 on the ultra-fine grid from data     |
!  |   previously generated by genfg.f90                                   |
!  -------------------------------------------------------------------------

use constants
implicit none

integer,parameter:: mgv=16,ntv=mgv*nt, ngv=mgv*ng
 !mgv:       Ultra-fine conversion grid ratio
 !ntv,ngv:   number of grid boxes in the x & y directions

real:: tr4,zzr4(ngv,ntv)
double precision:: zz(ngv,ntv),rdtv(ngv)
double precision:: aspsqm1,dlv,rhov,rsumv,dsumvi,ens
integer:: i,j,period
character(len=3):: pind

!---------------------------------------------------------------
 !For computing surface integrals:
aspsqm1=asp**2-one
dlv=twopi/dble(ntv)
do j=1,ngv
  rhov=cos((dble(j)-f12)*dlv-hpi)
  rdtv(j)=rhov*sqrt(one+aspsqm1*rhov**2)
enddo

rsumv=f1112*(rdtv(1)+rdtv(ngv))
do j=2,ngv-1
  rsumv=rsumv+rdtv(j)
enddo
dsumvi=one/(rsumv*dble(ntv))

!---------------------------------------------------------------
 !Select time frame:
write(*,*) ' Time period (choose 0 for the initial one)?'
read(*,*) period
write(pind(1:3),'(i3.3)') period

 !Open input data file:
open(44,file='fine/qq'//pind//'.r4',form='unformatted',status='old')

 !Read data:
read(44) tr4,zzr4
close(44)
write(*,'(a,f12.5)') ' t = ',tr4

 !Convert to double precision:
zz=dble(zzr4)

 !Compute enstrophy:
ens=zero
do i=1,ntv
  ens=ens+f1112*(zz(1,i)**2*rdtv(1)+zz(ngv,i)**2*rdtv(ng))
  do j=2,ngv-1
    ens=ens+zz(j,i)**2*rdtv(j)
  enddo
enddo
ens=f12*dsumvi*ens

write(*,'(a,f14.9)') ' <q^2>/2 = ',ens

end program
