#!/usr/bin/env csh

#============================================================================#
# Runs diffview on any selected pair of fields in the evolution subdirectory
#============================================================================#

set n=$#argv

if ($n == 0 || $n == 1) then
   echo
   echo '  Usage:   ddv <field1> <field2> <options>'
   echo
   echo '  Shows the difference, field1 - field2.'
   echo
   echo '  Examples:'
   echo
   echo '  ddv pn bpn'
   echo '  ddv pn bpn -lims -0.001 0.001'
   echo '  ddv pn bpn -notitle -cb'
   echo
   echo '  (For all options, just type "diffview")'
   echo
   exit (-1)
else
   set f1=$1
   set f2=$2
endif

# Get resolution
set ng = `grep "ng=" src/parameters.f90 | awk -F= '{print $(NF-1)}' | awk -F, '{print $1}'`
@ nt = ( 2 * $ng )

if ($n != 2) then
   # Remove first two elements of argument list <field> to get <options>:
   shift
   shift
   diffview -ndim $nt $ng {evolution/}$f1{.r4} {evolution/}$f2{.r4} $argv &
else
   diffview -ndim $nt $ng {evolution/}$f1{.r4} {evolution/}$f2{.r4} &
endif

