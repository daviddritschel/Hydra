#!/usr/bin/env csh

#=========================================================================#
#   Special job setup script for investigating the evolution of a MHD
#   flow starting with a gaussian vortex in a uniform magnetic field.

#   Developed 29/9/2021 by D G Dritschel @ St Andrews
#=========================================================================#


# @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ #
#                 ===>   Default parameters   <===

set pi=3.14159265358979323846
set ng="512"                     # Inversion grid resolution in both x and y
set nq="40"                      # Number of PV contours in 0 < q < <q^2>/<|q|>
set cof=`echo "4 * $pi" | bc -l` # Coriolis frequency
set nnu=3                        # Hyperviscosity power
set cdamp="10.0"                 # cdamp*(f+|zeta|_rms)*(k/k_max)^(2*nnu)
                                 # is the hyperdiffusivity coefficient

#                 ===>   Default parameters   <===
# @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ #

# Specify resolution:
echo
echo -n ' Resolution, ng (default' $ng')? '
set var=$<
if ($var != "") set ng=$var

# Max number of contour nodes:
@ npm = ( 15625 * ( $ng / 32 ) * ( $ng / 32) )

# Specify inverse Rossby deformation length:
set kdef="4"
echo
echo -n ' Rossby deformation wavenumber, f/c (default' $kdef')? '
set var=$<
if ($var != "") set kdef=$var

# Compute short-scale gravity wave speed, c:
set cgw=`echo "scale=12; $cof/$kdef" | bc -l`

echo ' '
echo ' We consider a vortex with zeta(r,0) = eps*f*exp(-0.5*(r/R)^2), and'
echo ' choose the vortex radius to be R = 5*pi/32.'
echo ' '
#set eps=0`echo "scale=3; 1.6/($kdef*$kdef)" | bc -l`
set eps=0`echo "scale=6; 1/($kdef*$kdef)" | bc -l`
echo -n ' Enter the Rossby number, eps (default' $eps'): '
set var=$<
if ($var != "") set eps=$var

# Total simulation time
set tsim=`echo "scale=1; 25/$eps" | bc -l`
# Grid data save time increment:
set tgsave=`echo "scale=1; 0.5/$eps" | bc -l`
# Contour data save time increment:
set tcsave=`echo "scale=1; 5/$eps" | bc -l`

# Characteristic vortex radius, R = 5*pi/32:
set rad=`echo "scale=12; 5 * $pi / 32" | bc -l`
# With this radius and a diffusion length of dx/2, we have delta = 6.4/ng

# Set magnetic field parameters using defaults assumed for the 
# maximum vorticity, z0, and characteristic horizontal scale, rad:
set z0=`echo "scale=12; $eps * $cof" | bc -l`
# With z0 = 4*pi*eps, a vortex rotation period is 1/eps
set u0=`echo "scale=12; $z0*$rad/2" | bc -l`
# This is the azimuthal velocity at r = R

set glx=`echo "scale=10; 2 * $pi / $ng" | bc -l`  # Grid scale

# Specify dlen/dx where dlen is the diffusion length:
set dlen_nd="1.0"
# Compute diffusion length:
set dlen=`echo "scale=10; $dlen_nd*$glx" | bc -l`
# Compute non-dimensional diffusion length, delta = dlen/rad:
set delta=`echo "scale=5; $dlen/$rad" | bc -l`
# Compute magnetic diffusivity, eta:
set eta=`echo "scale=10; $z0*$dlen*$dlen" | bc -l`
# Compute magnetic Reynolds number, Rm:
set rm=`echo "scale=8; $u0*$rad/$eta" | bc -l`

echo ' '
echo ' Initially, we take a magnetic field B = (bx0*H/h, 0, bz0).'
echo ' '
echo ' The x component is specified through gamma = bx0/(u0*delta)'
echo ' where u0 is the maximum velocity and delta = d_len/R is a'
echo ' dimensionless diffusion length.'

echo ' '
set gamma="2.0"
echo -n ' Enter gamma (default' $gamma'): '
set var=$<
if ($var != "") set gamma=$var
set bx0=`echo "scale=10; $gamma*$u0*$delta" | bc -l`

echo ' '
echo ' The z component bz0 is specified through the ratio bz0/u0.'

echo ' '
set bzrat="0.0"
echo -n ' Enter bz0/u0 (default' $bzrat'): '
set var=$<
if ($var != "") set bzrat=$var
set bz0=`echo "scale=10; $bzrat*$u0" | bc -l`

#==========================================================================
# Specify code numerical method class:
set meth="ca"
# Specify code geometry:
set geom="plane"
# Specify model equation type:
set equa="msw"
# Specify code version (typically OpenMP or not):
set algo="caps"

set local_home=`homedir`
set hydradir=$local_home/$user/hydra/$meth/$geom/$equa

# Get output base directory:
set workdir=`workdir`
# Make the base data directory if it does not already exist:
if (!(-d $workdir)) mkdir $workdir
set workdir=$workdir/hydra
if (!(-d $workdir)) mkdir $workdir
set basedir=$workdir/$meth
if (!(-d $basedir)) mkdir $basedir
set basedir=$basedir/$geom
if (!(-d $basedir)) mkdir $basedir
set basedir=$basedir/$equa
if (!(-d $basedir)) mkdir $basedir
set basedir=$basedir/$algo
if (!(-d $basedir)) mkdir $basedir

set outdir=$workdir/$meth/$geom/$equa/$algo

# Assign source and binary directories:
set srcdir=$hydradir/$algo
set bindir=$hydradir/scripts

# Get the name of the computer to record in the job_info file below:
set host=`hostname | awk -F. '{print $(1)}'`

echo ' '
echo ' ===> Building job <==='

#-------------------------------------------------------------------
# generate a random number seed based on current time:
set second=`date +%-S`
set minute=`date +%-M`
set hour=`date +%-H`
set day=`date +%-d`
@ iseed = ( $second + ( 60 * ( $minute + 60 * ( $hour + ( 24 * $day ) ) ) ) )

# Create a temporary directory:
set tmpdir=$outdir/tmp{$iseed}
mkdir $tmpdir
cd $tmpdir

# Set the job directory name:
set jobdir=ng{$ng}kd{$kdef}eps{$eps}gamma{$gamma}bzrat{$bzrat}

# Create a file containing scales for use in "scale" script:
echo $z0 $rad $u0 $delta $gamma $bzrat > in_scale

# Create a job information summary file:
touch job_info
echo ' Job created at                      ' `date` >> job_info
echo ' on                                  ' $host >> job_info
echo ' ' >> job_info
echo ' Inversion grid resolution in x & y: ' $ng >> job_info
echo ' ' >> job_info
echo ' Characteristic flow speed, U_0:     ' $u0 >> job_info
echo ' Characteristic length, L:           ' $rad >> job_info
echo ' Maximum vorticity, z_0:             ' $z0 >> job_info
echo ' ' >> job_info
echo ' Mag. diffusion length/dx, dlen/dx:  ' $dlen_nd >> job_info
echo '                dlen/R, delta:       ' $delta >> job_info
echo '      (Bx_0/U_0)/delta, gamma:       ' $gamma >> job_info
echo '      (Bz_0/U_0), bzrat              ' $bzrat >> job_info
echo ' Magnetic diffusivity, eta:          ' $eta >> job_info
echo ' Mean magnetic field in x, Bx_0:     ' $bx0 >> job_info
echo ' Mean magnetic field in z, Bz_0:     ' $bz0 >> job_info
echo ' Magnetic Reynolds number, U_0*R/eta:' $rm >> job_info

# Create input file for data generation routine:
set datagen = "gauss"
cat << /EOF > in_$datagen
$eps
$rad
$bx0
$bz0
/EOF

#============================================================
# Build parameter file with cpp and make all codes:
mkdir src
cd src
cp $srcdir/* .
cp -r $hydradir/init .
#cp -r $hydradir/post .

echo 
echo " Compiling source files....."
echo " -----------------------------------------------------------------------"

# Use C pre-processor to put chosen parameters $copts into parameters.f90
set copts="-DN_G=$ng -DN_CONTQ=$nq -DNPT_MAX=$npm -DT_SIM=$tsim -DT_GSAVE=$tgsave -DT_CSAVE=$tcsave -DCOR_FREQ=$cof -DC_GW=$cgw -DMAG_DIFF=$eta -DC_DAMP=$cdamp -DPOW_HYPER=$nnu"
precomp $copts parameters.f90

# Compile all codes and move sources to sub-directory:
#make caps setup $datagen proxy_post_all install
make caps setup $datagen install

cd ..
echo " -----------------------------------------------------------------------"
echo 

#============================================================
# Execute the data generation routine:
echo $datagen < in_$datagen > out_$datagen
$datagen < in_$datagen > out_$datagen

# Copy useful scripts to job directory:
/bin/cp $bindir/sv .
/bin/cp $bindir/dv .
/bin/cp $bindir/ddv .
/bin/cp $bindir/image.py .
/bin/cp $bindir/pene.py .
/bin/cp $bindir/ptene.py .

# Possibly create a directory named after the data generation script:
cd ..
if (!(-d $datagen)) mkdir $datagen
cd $datagen

# Move temporary directory to job directory:
/bin/mv $tmpdir $jobdir
cd $jobdir
set datadir=`pwd`

echo ' Launching job in the directory'
echo $datadir

cd $datadir

bat log caps
